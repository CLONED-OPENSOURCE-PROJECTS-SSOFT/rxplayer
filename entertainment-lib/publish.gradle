apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'org.jetbrains.dokka'

version '1.0.1'
group 'yuriykulikov.rxplayer'

/*
* Example here:
* https://github.com/bintray/bintray-examples/blob/master/gradle-bintray-plugin-examples/publications-example/build.gradle
* https://docs.gradle.org/current/userguide/publishing_maven.html#publishing_maven:complete_example
*/

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

dokka {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task javadocJar(type: Jar) {
    dependsOn dokka
    from dokka
    classifier = 'javadoc'
}

publishing {
    publications {
        Development(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId group
            artifactId 'entertainment-lib'
            version this.version
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['Development']
    pkg {
        repo = 'rxplayer'
        name = 'entertainment-lib'
        issueTrackerUrl = 'https://github.com/yuriykulikov/rxplayer/issues'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/yuriykulikov/rxplayer.git'
        description = 'Example Media Player with Rx interfaces'
        override = true
        publish = true
        version {
            name = this.version
            desc = "RxPlayer entertainment-lib ${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
}